import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def APP_ID = 'com.simpleapps.pingpong'
def APP_VERSION = '1.3'
def APP_CODE = 4
def FLAVOR_DIM = 'market'

android {
    namespace = "com.simpleapps.pingpong"
    flavorDimensions = [FLAVOR_DIM]

    defaultConfig {
        applicationId APP_ID
        minSdkVersion SDK_MIN
        targetSdkVersion SDK_MAX
        compileSdk SDK_MAX
        buildToolsVersion = BUILD_TOOL
        versionCode APP_CODE
        versionName APP_VERSION
        multiDexEnabled true

        buildConfigField "String", "LIB_GAME", "\"$LIB_GAME\""

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang",
                          "-DANDROID_STL=c++_static",
                          "-DLIB_GAME=$LIB_GAME"
                cppFlags "-std=c++17 -fexceptions -frtti"
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    signingConfigs {
        market {
            def keystoreProperties = getKeystore("keystore.properties")
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
            storeFile = file(keystoreProperties['storeFile'])
            storePassword = keystoreProperties['storePassword']
        }
    }

    productFlavors {
        dev {
            dimension FLAVOR_DIM
            applicationId "${APP_ID}.beta"
            versionName "${APP_VERSION}b"
        }

        prod {
            dimension FLAVOR_DIM
            applicationId APP_ID
            versionName APP_VERSION
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            productFlavors.prod.signingConfig signingConfigs.market
        }

        debug {
            minifyEnabled false
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                def buildType = variant.buildType.isDebuggable()? '_debug' : ''
                def dateTime =  new SimpleDateFormat("_MMMMM.dd_HH-mm").format(new Date())
                outputFileName = "Ping-Pong_${variant.versionName}" +
                                 "${buildType}${dateTime}_" +
                                 "build-${variant.versionCode}.apk"
            }
        }
    }

    sourceSets {
        main {
            java.srcDirs = [
                "src/main/java"
            ]

            jni.srcDirs = [
                "src/main/cpp"
            ]

            jniLibs.srcDirs = [
                "../lib"
            ]
        }
    }

    externalNativeBuild {
        cmake {
            version '3.10.2'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
    }

    packagingOptions {
        pickFirst "lib/x86_64/lib${LIB_GAME}.so"
        pickFirst "lib/x86/lib${LIB_GAME}.so"
        pickFirst "lib/armeabi-v7a/lib${LIB_GAME}.so"
        pickFirst "lib/arm64-v8a/lib${LIB_GAME}.so"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.0'

    implementation platform('com.google.firebase:firebase-bom:33.2.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
}